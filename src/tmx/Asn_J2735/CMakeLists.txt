# Use these lines and rebuild for debug information

PROJECT ( asn_j2735 C )
option(SAEJ2735_SPEC_VERSION "Version of the SAE J2735 specification." "2020" )
SET (ASN_J2735_DIR "${CMAKE_CURRENT_SOURCE_DIR}")

FILE (GLOB SOURCE_DIRS "${ASN_J2735_DIR}/src/*")
LIST (SORT SOURCE_DIRS)
FOREACH (SOURCE_DIR ${SOURCE_DIRS})
    GET_FILENAME_COMPONENT(SAEJ2735_SPEC "${SOURCE_DIR}" NAME)
    # r41, r63, 2020
    SET (LIBRARY_NAME ${PROJECT_NAME}_${SAEJ2735_SPEC})
    # asn_j2735_r41, asn_j2735_r63, asn_j2735_2020
    SET (${LIBRARY_NAME}_INCLUDE_DIR "${ASN_J2735_DIR}/include/asn_j2735_${SAEJ2735_SPEC}")
    
    FILE (GLOB_RECURSE ${LIBRARY_NAME}_SOURCES "${SOURCE_DIR}/*.c*")
    STRING (SUBSTRING ${SAEJ2735_SPEC} 1 -1 SAEJ2735_SPEC_DEF)
    # STRING() SAEJ2735_SPEC_DEF -> 41, 63, 2020
    ADD_LIBRARY (${LIBRARY_NAME} SHARED ${${LIBRARY_NAME}_SOURCES})
    TARGET_INCLUDE_DIRECTORIES (${LIBRARY_NAME} PUBLIC
    			$<BUILD_INTERFACE:${${LIBRARY_NAME}_INCLUDE_DIR}>
    			$<INSTALL_INTERFACE:include/asn_j2735_${SAEJ2735_SPEC}>)
    # same as #define SAEJ2735_SPEC=${SAEJ2735_SPEC_DEF}
    TARGET_COMPILE_DEFINITIONS (${LIBRARY_NAME} PUBLIC "-DSAEJ2735_SPEC=${SAEJ2735_SPEC_DEF}")
    MESSAGE (STATUS "Building SAE J2735 ${SAEJ2735_SPEC} specification to lib${LIBRARY_NAME}")

    IF (ASN_DEBUG)
        TARGET_COMPILE_DEFINITIONS (${LIBRARY_NAME} PRIVATE "-DEMIT_ASN_DEBUG=1")
    ENDIF (ASN_DEBUG)

    IF (TMX_LIB_DIR)
        SET_TARGET_PROPERTIES (${LIBRARY_NAME} PROPERTIES LIBRARY_OUTPUT_DIRECTORY "${TMX_LIB_DIR}")
    ENDIF ()
    
    SET (ASN_J2735_LIBRARIES ${LIBRARY_NAME} PARENT_SCOPE)

    INSTALL (TARGETS ${LIBRARY_NAME} EXPORT ${TMX_APPNAME}
             DESTINATION lib COMPONENT lib${LIBRARY_NAME})
    INSTALL (DIRECTORY ${${LIBRARY_NAME}_INCLUDE_DIR} 
             DESTINATION include COMPONENT lib${LIBRARY_NAME}
             FILES_MATCHING PATTERN "*.h*")
ENDFOREACH ()

UNSET (LIBRARY_NAME)
UNSET (SAEJ2735_SPEC)
# Used to set SAEJ2735_SPEC indirectly via target_compile_definitions. This definition is inherited by anything
# that links ASN_J2735_LIBRARIES as a dependency
SET (ASN_J2735_LIBRARIES ${PROJECT_NAME}_${SAEJ2735_SPEC_VERSION} PARENT_SCOPE)
